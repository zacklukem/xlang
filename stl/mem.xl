--* C realloc function
fun realloc(ptr: *void, size: usize) -> *void {
    let out = null as *void;
    @c(var ptr: $ptr, var size: $size ; $out: var out ; "
        $out = realloc($ptr, $size);
    ");
    ret out;
}

--* C malloc function
fun malloc(size: usize) -> *void {
    let out = null as *void;
    @c(var size: $size ; $out: var out ; "
        $out = malloc($size);
    ");
    ret out;
}

--* C free function
fun free(ptr: *void) {
    @c(var ptr: $ptr ;; "
        free($ptr);
    ");
}

--* Get the size of the type T
fun<T> sizeof() -> usize {
    let out = 0 as usize;
    @c(type T: $T; $out: var out ; "
        $out = sizeof($T);
    ");
    ret out;
}

--* Resize an array of type T.  This may change the pointer
fun<T> resize(ptr: **T, len: usize) {
    *ptr = realloc(*ptr, len * sizeof::<T>()) as *T;
}

--* Allocate an array with size `len` (`len * sizeof::<T>()` bytes)
fun<T> allocate(len: usize) -> *T {
    ret malloc(len * sizeof::<T>()) as *T;
}

--* Get a pointer by the given pointer offset
fun<T> ptr_at(ptr: *T, idx: usize) -> *T {
    let out = null as *T;
    @c(var ptr: $ptr, var idx: $idx ; $out: var out ; "
        $out = &$ptr[$idx];
    ");
    ret out;
}
